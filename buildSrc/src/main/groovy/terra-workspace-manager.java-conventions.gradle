// Java conventions common for all of the code
plugins {
    id 'idea'
    id 'java'
    id 'jacoco'
    id 'com.diffplug.spotless'
    id 'com.github.spotbugs'
}

def useMavenLocal = true
repositories {
    if (useMavenLocal) {
        mavenLocal()
    }
    maven {
        // Terra proxy for maven central
        url "https://broadinstitute.jfrog.io/broadinstitute/maven-central/"
    }
    mavenCentral()
    maven {
        url "https://broadinstitute.jfrog.io/broadinstitute/libs-release/"
    }
    maven {
        url "https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/"
    }
}

dependencyLocking {
    lockAllConfigurations()
    lockMode = LockMode.STRICT
}

jacoco {
    toolVersion = '0.8.8'
}

// The default jacocoTestReport step cannot handle inputs from multiple gradle
// tasks. Instead, we define our own task to generate a combined report from the
// output of all tasks (unitTest, connectedTest, azureTest, and even bootRun for
// integration tests) we've run.
task combinedJaCoCoReport(type: JacocoReport) {
    executionData fileTree("$buildDir/jacoco").include("*.exec")
    classDirectories.setFrom(files(project.sourceSets.main.output))
    sourceDirectories.setFrom(files(project.sourceSets.main.allSource.srcDirs))

    reports {
        xml.enabled(true)
        xml.destination = file("$buildDir/reports/jacoco/test/jacoco.xml")
    }

    // Ignore coverage of generated code.
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: 'bio/terra/workspace/generated/**')
        }))
    }

}

version = gradle.wsmVersion
group = 'bio.terra'
sourceCompatibility = JavaVersion.VERSION_17

// Spotless configuration
spotless {
    java {
        googleJavaFormat()
        targetExclude("${buildDir}/**", "**/generated/**")
    }
}
build.dependsOn spotlessApply


// for scans
if (hasProperty("buildScan")) {
    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"
    }
}

// Spotbugs configuration
spotbugs {
    toolVersion = '4.7.3'
    effort = 'max'
    excludeFilter = file("$rootDir/gradle/config/spotbugs/exclude.xml")
}
// The latest version of spotbugs still pulls in a vulnerable dependency.
// This can be removed when a new spotbugs version is available.
dependencies {
    constraints {
        spotbugs 'org.apache.bcel:bcel:6.6.1'
    }
}
spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main.html")
            stylesheet = 'fancy.xsl'
        }
    }
}
spotbugsTest {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/test.html")
            stylesheet = 'fancy.xsl'
        }
    }
}
