// Java Library conventions common for all of the code
plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.artifactory'
    id 'terra-workspace-manager.java-conventions'
}

group = 'bio.terra'
sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    google()
}

// Modify the standard :test task to only include unit-tagged tests.
test {
    useJUnitPlatform {
        includeTags 'unit'
    }
}

java {
    // Builds Javadoc into the published package as part of the 'assemble' task.
    withJavadocJar()
    // Builds sources into the published package as part of the 'assemble' task.
    withSourcesJar()
}

publishing {
    publications {
        workspaceManagerClientLibrary(MavenPublication) {
            artifactId = "workspace-manager-client"
            from components.java
            versionMapping {
                usage("java-runtime") {
                    fromResolutionResult()
                }
            }
        }
    }
}