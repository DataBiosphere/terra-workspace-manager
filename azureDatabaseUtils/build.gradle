// Project Plugins
plugins {
    id 'terra-workspace-manager.java-conventions'
    id "com.github.ben-manes.versions" version "0.51.0"
    id "com.google.cloud.tools.jib" version "3.4.0"
    id "de.undercouch.download" version "5.5.0"
    id "org.springframework.boot"
    id "io.spring.dependency-management"
    id 'ru.vyarus.quality' version '4.9.0'
    id 'org.gradle.test-retry' version '1.5.8'
    id "org.sonarqube" version "4.4.1.3373"
}

// constants visible to all .gradle files in this project
project.ext {
    artifactGroup = "${group}.workspace"
    includeDir = "$projectDir/gradle"
    openapiOutputDir = "${buildDir}/openapi"
    resourceDir = "${projectDir}/src/main/resources"

    // where to place the Cloud Profiler agent in the container
    cloudProfilerLocation = "/opt/cprof"

    // location for jib extras, including the Java agent
    jibExtraDirectory = "${buildDir}/jib-agents"
}

// include order matters, so don't alphabetize
apply(from: "$includeDir/deploy.gradle")
apply(from: "$rootDir/gradle/quality.gradle")
apply(from: "$includeDir/taskDependencies.gradle")
apply(from: "$includeDir/dependencies.gradle")

sonar {
    properties {
        property 'sonar.projectName', 'terra-workspace-manager'
        property 'sonar.projectKey', 'DataBiosphere_terra-workspace-manager'
        property 'sonar.organization', 'broad-databiosphere'
        property 'sonar.host.url', 'https://sonarcloud.io'
    }
}

test {
    useJUnitPlatform()
}
