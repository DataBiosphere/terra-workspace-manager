// Build Script Classpath
buildscript {
    ext {
        springBootVersion = '2.4.0'
    }
}

// Project Plugins
plugins {
    id 'terra-workspace-manager.java-conventions'
    id "com.github.ben-manes.versions" version "0.36.0"
    id "com.google.cloud.tools.jib" version "3.0.0"
    id "de.undercouch.download" version "4.1.1"
    id "org.hidetake.swagger.generator" version "2.18.2"
    id "org.springframework.boot" version "${springBootVersion}"
    id "io.spring.dependency-management" version "1.0.10.RELEASE"
    id 'ru.vyarus.quality' version '4.5.0'
}

// constants visible to all .gradle files in this project
project.ext {
    artifactGroup = "${group}.workspace"
    includeDir = "$projectDir/gradle"
    openapiOutputDir = "${buildDir}/openapi"
    resourceDir = "${projectDir}/src/main/resources"

    // where to place the Cloud Profiler agent in the container
    cloudProfilerLocation = "/opt/cprof"

    // location for jib extras, including the Java agent
    jibExtraDirectory = "${buildDir}/jib-agents"
}

// include order matters, so don't alphabetize
apply(from: "$includeDir/profiler.gradle")
apply(from: "$includeDir/deploy.gradle")
apply(from: "$includeDir/openapi.gradle")
apply(from: "$includeDir/versionProperties.gradle")
apply(from: "$rootDir/gradle/quality.gradle")
apply(from: "$includeDir/taskDependencies.gradle")
apply(from: "$includeDir/dependencies.gradle")
apply(from: "$includeDir/testing.gradle")

// Needed for ParameterNamesModule in StairwayMapper. See
// https://github.com/FasterXML/jackson-modules-java8/tree/master/parameter-names#:~:text=class%20Person%20must%20be%20compiled%20with%20Java%208%20compliant%20compiler%20with%20option%20to%20store%20formal%20parameter%20names%20turned%20on%20(%2Dparameters%20option).
tasks.withType(JavaCompile) {
    options.compilerArgs.addAll(['-parameters'])
}
