import java.time.ZonedDateTime

buildscript {
    apply(from: "$rootDir/common.gradle")
}

plugins {
    id "idea"
    id "java"
    id 'jacoco'

    id "com.diffplug.spotless" version "5.12.4"
    id "com.github.ben-manes.versions" version "0.36.0"
    id "com.google.cloud.tools.jib" version "3.0.0"
    id "de.undercouch.download" version "4.1.1"
    id "org.hidetake.swagger.generator" version "2.18.2"
    id "org.springframework.boot" version "${springBootVersion}"
    id "io.spring.dependency-management" version "1.0.10.RELEASE"
    id 'ru.vyarus.quality' version '4.5.0'
}

project.group = 'bio.terra'
project.version = gradle.wsmVersion
project.sourceCompatibility = JavaVersion.VERSION_11

project.ext {
    artifactGroup = "${group}.workspace"
    openapiOutputDir = "${buildDir}/openapi"
    resourceDir = "${projectDir}/src/main/resources"
    springBootVersion = '2.4.0'
}

def includeDir = "$projectDir/gradle"
// include order matters, so don't alphabetize (yet)
apply(from: "$includeDir/buildScan.gradle")
apply(from: "$includeDir/profiler.gradle")
apply(from: "$includeDir/testing.gradle")
apply(from: "$includeDir/deploy.gradle")
apply(from: "$includeDir/lint.gradle")
apply(from: "$includeDir/openapi.gradle")
apply(from: "$includeDir/versionProperties.gradle")
apply(from: "$includeDir/dependencies.gradle")

// Manual Task Dependencies
tasks.compileJava.dependsOn(swaggerSources.server.code, spotlessApply, generateVersionProperties)
tasks.jib.dependsOn extractProfilerAgent
tasks.jibDockerBuild.dependsOn extractProfilerAgent
tasks.jibBuildTar.dependsOn extractProfilerAgent
tasks.clean.dependsOn cleanVersionProperties
