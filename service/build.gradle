// Project Plugins
plugins {
    id 'terra-workspace-manager.java-spring-conventions'
    id "com.github.ben-manes.versions" version "0.52.0"
    id "com.google.cloud.tools.jib" version "3.4.5"
    id "de.undercouch.download" version "5.6.0"
    id "org.hidetake.swagger.generator" version "2.19.2"
    id 'org.gradle.test-retry' version '1.6.2'
    id "org.sonarqube" version "6.0.1.5171"
    id "au.com.dius.pact" version "4.6.17"
}

// constants visible to all .gradle files in this project
project.ext {
    artifactGroup = "${group}.workspace"
    includeDir = "$projectDir/gradle"
    openapiOutputDir = "${buildDir}/openapi"
    resourceDir = "${projectDir}/src/main/resources"

    // where to place the Cloud Profiler agent in the container
    cloudProfilerLocation = "/opt/cprof"

    // location for jib extras, including the Java agent
    jibExtraDirectory = "${buildDir}/jib-agents"
}

// include order matters, so don't alphabetize
apply(from: "$includeDir/profiler.gradle")
apply(from: "$includeDir/deploy.gradle")
apply(from: "$includeDir/openapi.gradle")
apply(from: "$includeDir/versionProperties.gradle")
apply(from: "$includeDir/taskDependencies.gradle")
apply(from: "dependencies.gradle")
apply(from: "$includeDir/testing.gradle")

// A small wrapper around bootRun to run the server instrumented with Jacoco
// code coverage tracking. This is useful for running a local server for
// integration tests, but live environments should use `bootRun` instead and
// should not be instrumented.
// This must live in the same file that the Spring Boot plugin is applied in or
// else it can't find the task type.
def googleCredentialsFile = "${rootDir}/config/wsm-sa.json"
task jacocoBootRun(type: org.springframework.boot.gradle.tasks.run.BootRun) {
    environment.put("GOOGLE_APPLICATION_CREDENTIALS", "${googleCredentialsFile}")
    classpath = sourceSets.main.runtimeClasspath
    mainClass = "bio.terra.workspace.app.Main"
}
jacoco {
    applyTo jacocoBootRun
}

sonar {
    properties {
        property 'sonar.projectName', "${rootProject.name}_${projectDir.name}"
        property 'sonar.projectKey', 'DataBiosphere_terra-workspace-manager'
        property 'sonar.organization', 'broad-databiosphere'
        property 'sonar.host.url', 'https://sonarcloud.io'
    }
}
