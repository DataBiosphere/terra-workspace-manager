// Library Dependencies, Versions, etc.
dependencies {
  // Google dependencies
  constraints {
    implementation 'com.google.guava:guava:31.1-jre' // "-jre" for Java 8 or higher
  }
  implementation platform('com.google.cloud:libraries-bom:25.1.0')
  implementation group: "com.google.auto.value", name: "auto-value-annotations"
  implementation group: "com.google.guava", name:"guava"

  // Azure dependencies
  implementation 'com.azure:azure-storage-blob:12.17.1'

  // AWS dependencies
  implementation platform('com.amazonaws:aws-java-sdk-bom:1.12.330')
  implementation group: "com.amazonaws", name: 'aws-java-sdk-core'
  implementation group: "com.amazonaws", name: 'aws-java-sdk-s3'
  implementation group: "com.amazonaws", name: 'aws-java-sdk-sagemaker'
  implementation group: "com.amazonaws", name: 'aws-java-sdk-sts'

  // Terra deps
  implementation group: "bio.terra", name: "datarepo-client", version: "1.41.0-SNAPSHOT"
  implementation group: "bio.terra", name:"billing-profile-manager-client", version: "0.1.29-SNAPSHOT"

  // hk2 is required to use datarepo client, but not correctly exposed by the client
  implementation group: "org.glassfish.jersey.inject", name: "jersey-hk2", version: "2.32"

  // Opencensus utilities for various HTTP clients
  implementation group: "io.opencensus", name: "opencensus-contrib-http-jaxrs", version: "0.28.3"

  // Get stairway via TCL
  implementation("bio.terra:terra-common-lib:0.0.69-SNAPSHOT") {
    exclude group: "org.broadinstitute.dsde.workbench", module: "sam-client_2.12"
  }
  implementation group: "org.broadinstitute.dsde.workbench", name: "sam-client_2.13", version: "0.1-e826d4d"
  implementation group: "bio.terra", name: "terra-resource-buffer-client", version: "0.4.3-SNAPSHOT"
  // Required by Sam client
  implementation group: "com.squareup.okhttp3", name: "okhttp", version: "4.9.1"
  implementation group: "com.squareup.okhttp3", name: "logging-interceptor", version: "4.9.1"

  // Cloud Resource Library
  implementation group: 'bio.terra', name: 'terra-cloud-resource-lib', version: "1.2.5-SNAPSHOT"

  // Terra Policy Service
  implementation('bio.terra:terra-policy-service:0.2.11-SNAPSHOT') {
    exclude group: "org.broadinstitute.dsde.workbench", module: "sam-client_2.12"
  }

  // Terra Landing Zone Service
  implementation ('bio.terra:terra-landing-zone-service:0.0.20-SNAPSHOT') {
    exclude group: "org.broadinstitute.dsde.workbench", module: "sam-client_2.12"
  }

  // Storage transfer service
  implementation group: 'com.google.apis', name: 'google-api-services-storagetransfer', version: 'v1-rev20220324-1.32.1'
  implementation group: "com.google.auth", name: "google-auth-library-oauth2-http", version: "1.6.0"

  // Versioned direct deps
  implementation group: "org.hashids", name: "hashids", version: "1.0.3"
  implementation group: "com.fasterxml.jackson.core", name: "jackson-core", version: "2.13.2"
  implementation group: "org.liquibase", name: "liquibase-core", version: "4.8.0"
  implementation group: "org.webjars", name: "webjars-locator-core", version: "0.46"
  runtimeOnly group: "org.postgresql", name: "postgresql", version: "42.3.3"

  // Deps whose versions are controlled by Spring
  implementation group: "javax.validation", name: "validation-api"
  implementation group: "org.apache.commons", name: "commons-dbcp2"
  implementation group: "org.apache.commons", name: "commons-lang3"
  implementation group: "org.apache.commons", name: "commons-pool2"
  implementation group: "org.apache.logging.log4j", name: "log4j-api"
  implementation group: "org.apache.logging.log4j", name: "log4j-to-slf4j"
  implementation group: "commons-validator", name: "commons-validator", version: "1.7"
  implementation group: "org.springframework.boot", name: "spring-boot-starter-data-jdbc"
  implementation group: "org.springframework.boot", name: "spring-boot-starter-web"
  implementation group: "org.springframework.boot", name: "spring-boot-starter-validation"
  implementation group: "org.springframework.retry", name: "spring-retry"

  // OpenAPI (swagger) deps
  implementation gradle.librarySwaggerAnnotations
  swaggerCodegen gradle.librarySwaggerCli
  runtimeOnly group: "org.webjars.npm", name: "swagger-ui-dist", version: "3.37.2"

  // Spotbugs dependency
  // For SpotBugs annotations
  compileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugs.toolVersion.get()}"
  spotbugs "com.github.spotbugs:spotbugs:${spotbugs.toolVersion.get()}"

  // Test deps
  testImplementation group: "io.vavr", name: "vavr", version: "0.10.3"
  testImplementation('org.springframework.boot:spring-boot-starter-test') {
    exclude group: 'com.vaadin.external.google', module: 'android-json'
  }
  testImplementation group: "org.hamcrest", name: "hamcrest", version: "2.2"
  // Allows us to mock final classes
  testImplementation 'org.mockito:mockito-inline:2.13.0'
  testImplementation 'com.google.cloud:google-cloud-bigquery'

  testImplementation "org.testcontainers:testcontainers:1.17.5"
  testImplementation "org.testcontainers:junit-jupiter:1.17.5"
  testImplementation "org.testcontainers:postgresql:1.17.5"

  annotationProcessor group: "com.google.auto.value", name: "auto-value", version: "1.7.4"
  annotationProcessor group: "org.springframework.boot", name: "spring-boot-configuration-processor", version: "2.6.6"
}
